  100 ! Let's check if we use TIIF2 or HEXTIr
  110 CALL IO(45,1,S):IF S=4 THEN DN$="101." ELSE DN$="100."
  120 !ON BREAK ERROR:ON ERROR 130:GOTO 140
  130 !CLOSE #1:STOP
  140 MAX=50:RECSIZE=100
  150 PRINT "(c/r/s)":A$=CHR$(ASC(KEY$)AND NOT 32)
  160 IF A$="R" THEN 260 ELSE IF A$="S" THEN 350
  170 !Create the file
  180 OPEN #1,DN$&"testrel.txt",OUTPUT,DISPLAY,RELATIVE,VARIABLE RECSIZE
  190 RESTORE #1,REC 0
  200 FOR I=1 TO MAX
  210 PRINT I
  220 PRINT #1,I;",";I^2;",";SQR(I);",";SIN(I)
  230 NEXT I
  240 CLOSE #1
  250 ! read the file with random access
  260 OPEN #1,DN$&"testrel.txt",INPUT,DISPLAY,RELATIVE,VARIABLE RECSIZE
  270 RANDOMIZE
  280 IMAGE ### ### ###### ##.#### ##.####
  290 I=INT(RND*MAX)
  300 RESTORE #1,REC I
  310 INPUT #1,X,X1,X2,X3
  320 PRINT USING 280,I;X;X1;X2;X3
  330 GOTO 290
  340 ! read the file sequential until EOF
  350 OPEN #1,DN$&"testrel.txt",INPUT,DISPLAY,RELATIVE,VARIABLE RECSIZE
  360 RESTORE #1,REC 0
  370 IMAGE ### ###### ##.#### ##.####
  380 IF EOF(1) THEN CLOSE #1:STOP
  390 INPUT #1,X,X1,X2,X3
  400 PRINT USING 370,X;X1;X2;X3
  410 GOTO 380